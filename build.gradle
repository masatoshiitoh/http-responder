plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
  id 'maven'
}

group = 'jp.dressingroom.apiguard'
version = '1.0.0-SNAPSHOT'

repositories {
  mavenCentral()
}

ext {
  vertxVersion = '3.8.3'
  junitJupiterEngineVersion = '5.4.0'
}

application {
  mainClassName = 'io.vertx.core.Launcher'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

sourceCompatibility = '1.8'

def mainVerticleName = 'jp.dressingroom.apiguard.httpresponder.HttpResponderMainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation "io.vertx:vertx-dropwizard-metrics:$vertxVersion"
  implementation "io.vertx:vertx-redis-client:$vertxVersion"

  compile "io.vertx:vertx-web-client:$vertxVersion"
  compile "io.vertx:vertx-config:$vertxVersion"

  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testImplementation "io.vertx:vertx-unit:$vertxVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterEngineVersion"
}


shadowJar {
  classifier = 'fat'
  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

tasks.withType(AbstractCompile)*.options*.encoding = tasks.withType(GroovyCompile)*.groovyOptions*.encoding = 'UTF-8'

// MavenRepository identity setting リポジトリの設定。これらは当然、読み込み時に使用する
// group = 'com.kissaki.sample'	//グループ
archivesBaseName = 'Mavenrepobygradle' 	//アーカイブベース
// version = '0.0.1'		//バージョン

uploadArchives {//アーカイブに際して、Maven形式で、どこに出力するかを指定する箇所
  repositories {
    mavenDeployer {
      repository(url: "file:${projectDir}")
    }
  }
}

[install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
  pom.project {
    inceptionYear '2019'
    packaging 'jar'
    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }
  }
}
